#version 450
#extension GL_EXT_mesh_shader : require
#extension GL_GOOGLE_include_directive : require
// #extension GL_EXT_debug_printf : enable

#include "Loader/VertexDataLoader.glsl"
#include "Loader/MainCameraDataLoader.glsl"
#include "SSBO/StandardSSBO.glsl"

layout(local_size_x = 64) in;
layout(triangles, max_vertices = 64, max_primitives = 124) out;

struct SharedData
{
    uint objectIndex;
    uint meshIndex;
    uint meshletIndex;
};
taskPayloadSharedEXT SharedData sharedData;

layout(location = 0) out VertexOutput {
	vec4 color;
} vertexOutput[];

void main()
{
    // debugPrintfEXT("mesh index: %d", sharedData.meshIndex);
    MeshData meshData = VertexDataLoader_GetMeshData(sharedData.meshIndex);
    MeshletMetaData meshletMetaData = VertexDataLoader_GetMeshletMetaData(sharedData.meshletIndex);

    // 出力する頂点数・プリミティブ数を指定
    if (gl_LocalInvocationIndex == 0) {
        SetMeshOutputsEXT(meshletMetaData.vertexCount, meshletMetaData.primitiveCount);
    }
    barrier();

    MainCameraData cameraData = MainCameraDataLoader_GetMainCameraData();
    StandardSSBO standardSSBO = StandardSSBO_LoadStandardSSBO(sharedData.objectIndex);

    uint stride = gl_WorkGroupSize.x;
    uint idx = gl_LocalInvocationIndex;

    // 頂点処理：スレッドで分割し、forループで複数頂点を担当
    for (uint i = idx; i < meshletMetaData.vertexCount; i += stride) 
    {
        VertexData v = VertexDataLoader_GetVertexData(sharedData.meshIndex, sharedData.meshletIndex, i);
        vec4 worldPos = standardSSBO.model * v.position;
        gl_MeshVerticesEXT[i].gl_Position = cameraData.projection * cameraData.view * worldPos;
        vertexOutput[i].color = v.color;
    }

    // プリミティブ処理：同様に分割＋ループで処理
    for (uint i = idx; i < meshletMetaData.primitiveCount; i += stride) 
    {  
        uvec3 index = VertexDataLoader_GetPrimitivesData(sharedData.meshIndex, sharedData.meshletIndex, i);
        gl_PrimitiveTriangleIndicesEXT[i] = index;
    }
}
