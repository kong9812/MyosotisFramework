#version 450
#extension GL_EXT_mesh_shader : require
#extension GL_GOOGLE_include_directive : require
#extension GL_EXT_debug_printf : enable

#include "Loader/RawDataLoader.glsl"
#include "Loader/VertexDataLoader.glsl"
#include "Loader/MainCameraDataLoader.glsl"

layout(local_size_x = 64) in;
layout(triangles, max_vertices = 64, max_primitives = 124) out;

struct TaskShaderToMeshShaderData
{
    uint _using;
    uint objectIndex;
    uint meshIndex; //todo.Meshlet化
    uint meshletIndex;
};
layout (std430, set = 1, binding = 5) buffer TaskShaderToMeshShaderDataBuffer {
    TaskShaderToMeshShaderData taskShaderToMeshShaderDatas[];
};

layout(location = 0) out VertexOutput {
	vec4 color;
} vertexOutput[];

vec4 randomColor(float seed)
{
    // ハッシュ関数的に疑似乱数を生成
    float r = fract(sin(seed * 12.9898) * 43758.5453);
    float g = fract(sin((seed + 1.0) * 78.233) * 43758.5453);
    float b = fract(sin((seed + 2.0) * 39.425) * 43758.5453);
    return vec4(r, g, b, 1.0);
}

void main()
{
    // debugPrintfEXT("MeshShader!!!!!");

    TaskShaderToMeshShaderData taskShaderToMeshShaderData = taskShaderToMeshShaderDatas[gl_WorkGroupID.x];
    if (taskShaderToMeshShaderData._using == 0)
    {
        return;
    }

    barrier();

    MeshData meshData = VertexDataLoader_GetMeshData(taskShaderToMeshShaderData.meshIndex);
    MeshletMetaData meshletMetaData = VertexDataLoader_GetMeshletMetaData(taskShaderToMeshShaderData.meshIndex, taskShaderToMeshShaderData.meshletIndex);

    // 出力する頂点数・プリミティブ数を指定
    if (gl_LocalInvocationIndex == 0) {
        SetMeshOutputsEXT(meshletMetaData.vertexCount, meshletMetaData.primitiveCount);
    }
    barrier();

    MainCameraData cameraData = MainCameraDataLoader_GetMainCameraData();
    StandardSSBO standardSSBO = RawDataLoader_LoadStandardSSBO(taskShaderToMeshShaderData.objectIndex);

    uint stride = gl_WorkGroupSize.x;
    uint idx = gl_LocalInvocationIndex;

    // 頂点処理：スレッドで分割し、forループで複数頂点を担当
    for (uint i = idx; i < meshletMetaData.vertexCount; i += stride) 
    {
        uint index = VertexDataLoader_GetUniqueIndexData(taskShaderToMeshShaderData.meshIndex, taskShaderToMeshShaderData.meshletIndex, i);
        VertexData v = VertexDataLoader_GetVertexData(taskShaderToMeshShaderData.meshIndex, taskShaderToMeshShaderData.meshletIndex, index);
        vec4 worldPos = standardSSBO.model * v.position;
        gl_MeshVerticesEXT[i].gl_Position = cameraData.projection * cameraData.view * worldPos;
        vertexOutput[i].color = randomColor(taskShaderToMeshShaderData.meshletIndex);
    }

    // プリミティブ処理：同様に分割＋ループで処理
    for (uint i = idx; i < meshletMetaData.primitiveCount; i += stride) 
    {
        uvec3 index = VertexDataLoader_GetPrimitivesData(taskShaderToMeshShaderData.meshIndex, taskShaderToMeshShaderData.meshletIndex, i);
        
        if (taskShaderToMeshShaderData.meshletIndex == 5)
        {
            uint offset = meshletMetaData.primitivesOffset + (i * 3);
        }
        gl_PrimitiveTriangleIndicesEXT[i] = index;
    }

    taskShaderToMeshShaderData._using = 0;
}
