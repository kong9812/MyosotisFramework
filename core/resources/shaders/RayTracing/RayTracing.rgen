#version 460
#extension GL_EXT_ray_tracing : enable
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_GOOGLE_include_directive : require

#include "../Descriptors/CameraInfo.glsl"
#include "../Descriptors/TLAS.glsl"
#include "../Descriptors/BasicMaterialInfo.glsl"

layout (set = 3, binding = 1) writeonly uniform image2D Image2D[];

layout(push_constant) uniform PushConstant {
    uint storeImageID;
};

struct RayPayload { 
	vec3 color;
	float distance;
};

layout(location = 0) rayPayloadEXT RayPayload rayPayload;

void main() 
{
	BasicMaterialInfo test = BasicMaterialInfo_GetBasicMaterialInfo(0);

	const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
	const vec2 inUV = pixelCenter/vec2(gl_LaunchSizeEXT.xy);
	vec2 d = inUV * 2.0 - 1.0;

    // CameraInfo (MainCameraData)
    CameraData cameraData = CameraInfo_GetCameraData(CameraInfo_GetMainCameraIndex());

	vec4 target = cameraData.invProjection * vec4(d.x, d.y, 1, 1) ;
	vec3 dirView = normalize(target.xyz / target.w);
	vec4 direction = normalize(cameraData.invView * vec4(normalize(dirView), 0));

	float tmin = 0.001;
	float tmax = 10000.0;

    vec3 color = vec3(0.0);

    traceRayEXT(TLAS_GetTLAS(), gl_RayFlagsOpaqueEXT, 0xff, 0, 1, 0, cameraData.pos.xyz, tmin, direction.xyz, tmax, 0);
	color = rayPayload.color;
	imageStore(Image2D[storeImageID], ivec2(gl_LaunchIDEXT.xy), vec4(rayPayload.color, 0.0));
}
